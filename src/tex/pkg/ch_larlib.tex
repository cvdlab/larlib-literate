\chapter{Module overview}



%===============================================================================
\section{Data transforms and boundary operators}\label{sec:boundary}
%===============================================================================

In this section we provide some Julia functions to compute (boundary) operator representations starting from other data representations, i.e., generally speaking, starting from one/two incidence relations.

\paragraph{Sparse characteristic matrices $M_p$}

The generic function \texttt{characteristicMatrix} produces a compressed sparse column representation of a single incidence relation, i.e.~the associated characteristic matrix $M_p$ In this code we use $p=2$ and \texttt{FV} as parameter name, but it may refer to any other incidence relation as actual value. The function employs a COO (coordinate) method to build the sparse matrix, i.e.~starts from a triple of arrays for \texttt{I,J,V} values.


%-------------------------------------------------------------------------------
@D Computation of sparse characteristic matrices $M_p$
@{
# Characteristic matrix $M_2$, i.e. M(FV)
function characteristicMatrix(FV)
	I,J,V = Int64[],Int64[],Int8[] 
	for f=1:length(FV)
		for k in FV[f]
			push!(I,f)
			push!(J,k)
			push!(V,1)
		end
	end
	M_2 = sparse(I,J,V)
	return M_2
end
@}
%-------------------------------------------------------------------------------


Hence we get the sparse matrix \texttt{cscEV}, shown in Figure~\ref{fig:intro-0}, by 
{\small\begin{verbatim}
cscEV = characteristicMatrix(EV)
\end{verbatim}}

\paragraph{Boundary matrix $[\partial_1] = [\delta_0]^\top$}

It is worth noting that the matrix $M_1=\texttt{cscEV}$ previously computed is very similar to the operator matrix $[\delta_0]$, but with elements in $\{-1,0,1\}$ instead than in $\{0,1\}$. The computation is than easily done, by using the fact that the 1-cell is written as oriented 0-chains $\eta=\nu_k - \nu_h$, with $k>h$, and hence in coordinates we have $\texttt{spboundary1}[k,e] = 1$ and $\texttt{spboundary1}[k,e] = -1$.

%-------------------------------------------------------------------------------
@D Computation of (signed) sparse boundary $C_1 \to C_0$
@{
# Computation of sparse boundary $C_1 \to C_0$
function boundary1(EV)
	spboundary1 = LARLIB.characteristicMatrix(EV)'
	for e = 1:length(EV)
		spboundary1[EV[e][1],e] = -1
	end
	return spboundary1
end
@}
%-------------------------------------------------------------------------------

In this case we get the compressed sparse column matrix representation $\partial_1$, shown dense in Figure~\ref{fig:intro-2}, by computing
\[
\partial_1 = \small\texttt{LARLIB.boundary1(EV)}
\]
\begin{figure}[htbp] %  figure placement: here, top, bottom, or page
{\footnotesize\begin{verbatim}
julia> julia> full(LARLIB.boundary1(EV))
12×20 Array{Int8,2}:
 -1   0   0   0   0   0  -1   0   0   0   0   0  -1   0   0   0   0   0   0   0
  1   0   0   0   0   0   0  -1   0   0   0   0   0  -1   0   0   0   0   0   0
  0  -1   0   0   0   0   1   0   0   0   0   0   0   0  -1   0   0   0   0   0
  0   1   0   0   0   0   0   1   0   0   0   0   0   0   0  -1   0   0   0   0
  0   0  -1   0   0   0   0   0  -1   0   0   0   1   0   0   0  -1   0   0   0
  0   0   1   0   0   0   0   0   0  -1   0   0   0   1   0   0   0  -1   0   0
  0   0   0  -1   0   0   0   0   1   0   0   0   0   0   1   0   0   0  -1   0
  0   0   0   1   0   0   0   0   0   1   0   0   0   0   0   1   0   0   0  -1
  0   0   0   0  -1   0   0   0   0   0  -1   0   0   0   0   0   1   0   0   0
  0   0   0   0   1   0   0   0   0   0   0  -1   0   0   0   0   0   1   0   0
  0   0   0   0   0  -1   0   0   0   0   1   0   0   0   0   0   0   0   1   0
  0   0   0   0   0   1   0   0   0   0   0   1   0   0   0   0   0   0   0   1
\end{verbatim}}
   \caption{Dense signed $[\partial_1]$ matrix $\partial_1$ for the small complex in Figure~\ref{fig:intro-1}}
   \label{fig:intro-2}
\end{figure}


\paragraph{Boundary matrix $[\partial_2] = [\delta_1]^\top$}

The matrix of the unsigned operator $\partial_2$ is computed here, according to the method introduced in~\cite{}. In particular, the two sparse incidence matrices \texttt{cscFV} and \texttt{cscEV} are first computed, and the product matrix in \texttt{temp} codifies, for any pair $(i,j)$ of indices the number of vertices shared between the  $i$-th 2-cell and the $j$-th 1-cell; when this number is equal to 2, the $j$-th 1-cell belongs to the boundary of the $i$-th 2-cell, so providing a triple $(i,j,1)$ to be inserted in the COO representation of \texttt{sp\_uboundary2}, and finally in its CSC representation.

%-------------------------------------------------------------------------------
@D Computation of (unsigned) sparse boundary $C_2 \to C_1$
@{
# Computation of sparse uboundary2
function uboundary2(FV,EV)
	cscFV = characteristicMatrix(FV)
	cscEV = characteristicMatrix(EV)
	temp = cscFV * cscEV'
	I,J,V = Int64[],Int64[],Int8[]
	for j=1:size(temp,2)
		for i=1:size(temp,1)
			if temp[i,j] == 2
    			push!(I,i)
    			push!(J,j)
    			push!(V,1)
			end
		end
	end
	sp_uboundary2 = sparse(I,J,V)
	return sp_uboundary2
end
@}
%-------------------------------------------------------------------------------


\paragraph{Signed boundary matrix $[\partial_2] = [\delta_1]^\top$}

Here we build a possible value for $[\partial_2] \in \{-1,0,1\}^m_n$. By rows, it describes each elementary 2-chain (2-cell) as a signed 1-cycle (1-chain without boundary). Since no standard orientation exists for $p$-cycles in $d$-space ($p < d$),
\emph{by convention} we compute each 1-cycle oriented as its first 1-cell, i.e. going in the direction from the second to the first 0-cell of it.

The construction algorithm may be split into two phases: the initialization and the loop indexed on faces. In turn, the faces loop calls the function \texttt{columninfo(col)}, deputed to store a quadruple of data providing a data structure to set properly (in sign) each 1-cell of a 1-cycle.

\subparagraph{Initialization}
%-------------------------------------------------------------------------------
@D Initialization of the (signed) sparse boundary
@{
# Initialization
function boundary2(FV,EV)
    sp_u_boundary2 = LARLIB.uboundary2(FV,EV)
    larEV = LARLIB.characteristicMatrix(EV)
    # unsigned incidence relation
    FE = [findn(sp_u_boundary2[f,:]) for f=1:size(sp_u_boundary2,1) ]
    I,J,V = Int64[],Int64[],Int8[]
    vedges = [findn(larEV[:,v]) for v=1:size(larEV,2)]
@}
%-------------------------------------------------------------------------------

\subparagraph{Local storage}
A local function \texttt{columninfo} is used to store the needed data structure into a temporary array \texttt{infos}

%-------------------------------------------------------------------------------
@D Local temporary storage function
@{
# Local storage
function columninfo(infos,EV,next,col)
    infos[1,col] = 1
    infos[2,col] = next
    infos[3,col] = EV[next][1]
    infos[4,col] = EV[next][2]
    vpivot = infos[4,col]
end
@}
%-------------------------------------------------------------------------------

\subparagraph{Loop on faces}
%-------------------------------------------------------------------------------
@D Loop on faces to construct the (signed) sparse boundary2
@{# Loop on faces
for f=1:length(FE)
    fedges = Set(FE[f])
    next = pop!(fedges)
    col = 1
    infos = zeros(Int64,(4,length(FE[f])))
    vpivot = infos[4,col]
    vpivot = columninfo(infos,EV,next,col)
    while fedges != Set()
        nextedge = intersect(fedges, Set(vedges[vpivot]))
        fedges = setdiff(fedges,nextedge)
        next = pop!(nextedge)
        col += 1
        vpivot = columninfo(infos,EV,next,col)
        if vpivot == infos[4,col-1]
            infos[3,col],infos[4,col] = infos[4,col],infos[3,col]
            infos[1,col] = -1
            vpivot = infos[4,col]
        end
    end
    for j=1:size(infos,2)
        push!(I, f)
        push!(J, infos[2,j])
        push!(V, infos[1,j])
    end
end
@}
%-------------------------------------------------------------------------------

The assembly of the whole function \texttt{boundary2(FV,EV)} is given below.

\subparagraph{Assembly signed boundary construction}

%-------------------------------------------------------------------------------
@D Computation of (signed) sparse boundary $C_2 \to C_1$
@{
@< Local temporary storage function @>
@< Initialization of the (signed) sparse boundary @>
    @< Loop on faces to construct the (signed) sparse boundary2 @>
    spboundary2 = sparse(I,J,V)
    return spboundary2
end
@}
%-------------------------------------------------------------------------------

%===============================================================================
\section{Chain complex construction}\label{sec:chaincomplex}
%===============================================================================
In this section we introduce the main function used to construct the chain complex of a space arrangement starting from a LAR input that may contain one or more cellular complexes.
The input is a standard LAR model, i.e., a triple with vertex coordinates, and incidennce realtions of 2-cells and 1-cells with vertices.
The output is a triple made by new \emph{vertices}, the new \emph{bases} for 1-, 2-, and 3-cells, and the signed boundary operators $\partial_1, \partial_2, \partial_3$.
%-------------------------------------------------------------------------------
@D Chain 3-complex construction
@{# Chain 3-complex construction
function chaincomplex(W,FW,EW)
    V = convert(Array{Float64,2},W')
    EV = characteristicMatrix(EW)
    FE = boundary2(FW,EW)
    V,cscEV,cscFE,cscCF = LARLIB.spatial_arrangement(V,EV,FE)
    ne,nv = size(cscEV)
    nf = size(cscFE,1)
    nc = size(cscCF,1)
    EV = [findn(cscEV[e,:]) for e=1:ne]
    FV = [collect(Set(vcat([EV[e] for e in findn(cscFE[f,:])]...)))  for f=1:nf]
    CV = [collect(Set(vcat([FV[f] for f in findn(cscCF[c,:])]...)))  for c=2:nc]
    function ord(cells)
        return [sort(cell) for cell in cells]
    end
    temp = copy(cscEV')
    for k=1:size(temp,2)
        h = findn(temp[:,k])[1]
        temp[h,k] = -1
    end    
    cscEV = temp'
    bases, coboundaries = (ord(EV),ord(FV),ord(CV)), (cscEV,cscFE,cscCF)
    return V',bases,coboundaries
end
@}
%-------------------------------------------------------------------------------

\paragraph{Collect one or more LAR models in a single model}

The goal of the function \texttt{collection2model} is to store a collection of LAR models (i.e.~triples (W,FW,EW)) into a single LAR model, by concatenation of vertex arrays, and concatenation of incidence relations, properly shifted.
The input type is \texttt{Array{Array{Array,1},1}}, the output type is \texttt{Tuple{Array{Float64,2},Array{Array{Int64,1},1},Array{Array{Int64,1},1}}}.
Of course, the result may not be the LAR representation of a cellular complex, since some cells may intersect out of boundaries.
Actually, this function prepares the input to be submitted to the \texttt{chaincomplex} function, that provides to mutually intersect the 2-cells and to reconstruct the 3-cells using the topological gift-wrapping algorithm~\cite{DBLP:journals/corr/PaoluzziSD17}.

%-------------------------------------------------------------------------------
@D Collect LAR models in a single LAR model
@{# Collect LAR models in a single LAR model
function collection2model(collection)
	W,FW,EW = collection[1]
	shiftV = size(W,2)
	for k=2:length(collection)
		V,FV,EV = collection[k]
		W = [W V]
		FW = [FW; FV + shiftV]
		EW = [EW; EV + shiftV]
		shiftV = size(W,2)
	end
	return W,FW,EW
end
@}
%-------------------------------------------------------------------------------

\paragraph{Triangulation of a single 2-cell embedded in 3D}

The 2-cells of a LAR cellular complex may have any kind of geometry or topology, provided they are connected. In other words, the 2-cells may be either convex or no-convex, and either simply connected or multiply connected, i.e.~containing any finite number of internal holes. Of course, the topological gift-wrapping algorithm~cite{DBLP:journals/corr/PaoluzziSD17}, introduced to extract the basis of 3-cells as minimal cycles of 2-cells,
has no problems in handling such general kind of 2-cells. Conversely, in order to draw the complex, or to show its shape into an interactive viewer, a decomposition of the boundary of every 3-cell into a set of triangles (triple of 3-points) is needed, since this one is the only data structure accepted by the graphics hardware.

The following higher-level function \texttt{facetriangulation} computes this triangulation for the generic 2-cell indexed by \texttt{f}, taking as input (for the first application) also the vertices \texttt{V}, the incidence relation \texttt{FV}, and the coboundary operators $\delta_1$ and $\delta_2$. In particular, for each 2-cell $f$, a \emph{local} LAR representation \texttt{vts} is generated using a vertex dictionary \texttt{vdict} and its inverse map \texttt{dictv}. The 2-face is trasformed in the 2-subspace $z=0$ using the transformation matrix \texttt{inv(M)}, in order to prepare the types of data expected by the \texttt{TRIANGLE.constrained\_triangulation\_vertices} that performs a CDT (Constrained Delaunay Triangulation) algorithm so finally returning the incidence relation between triangle and (original) vertices named \texttt{mktriangles}.

%-------------------------------------------------------------------------------
@D Triangulation of a single facet
@{# Triangulation of a single facet
function facetriangulation(V,FV,EV,cscFE,cscCF)
	function facetrias(f)
		vs = [V[:,v] for v in FV[f]]
		vs_indices = [v for v in FV[f]]
		vdict = Dict([(i,index) for (i,index) in enumerate(vs_indices)])
		dictv = Dict([(index,i) for (i,index) in enumerate(vs_indices)])
		es = findn(cscFE[f,:])
	
		vts = [v-vs[1] for v in vs]
	
		v1 = vts[2]
		v2 = vts[3]
		v3 = cross(v1,v2)
		err, i = 1e-8, 1
		while norm(v3) < err
			v2 = vts[3+i]
			i += 1
			v3 = cross(v1,v2)
		end	
	
		M = [v1 v2 v3]

		vs_2D = hcat([(inv(M)*v)[1:2] for v in vts]...)'
		pointdict = Dict([(vs_2D[k,:],k) for k=1:size(vs_2D,1)])
		edges = hcat([[dictv[v] for v in EV[e]]  for e in es]...)'
	
		trias = TRIANGLE.constrained_triangulation_vertices(
			vs_2D, collect(1:length(vs)), edges)

		triangles = [[pointdict[t[1,:]],pointdict[t[2,:]],pointdict[t[3,:]]] 
			for t in trias]
		mktriangles = [[vdict[t[1]],vdict[t[2]],vdict[t[3]]] for t in triangles]
		return mktriangles
	end
	return facetrias
end
@}
%-------------------------------------------------------------------------------

\paragraph{Triangulation of the whole 2-skeleton}

The \texttt{cf} tuple, that constitute the main input of the \texttt{triangulate} function, contains the COO representation of the signed sparse matrix $[\delta_2]$.
The aim of \texttt{triangulate} is to produce the incidence relation \texttt{TV} between the oriented triangles \texttt{T} and the vertices \texttt{V} of the 3-skeleton of our cellular complex.

%-------------------------------------------------------------------------------
@D Triangulation of the 2-skeleton
@{# Triangulation of the 2-skeleton
function triangulate(cf,V,FV,EV,cscFE,cscCF)
	mktriangles = LARLIB.facetriangulation(V,FV,EV,cscFE,cscCF)
	TV = Array{Int64,1}[]
	for (f,sign) in zip(cf[1],cf[2])
		triangles = mktriangles(f)
		if sign == 1
			append!(TV,triangles )
		elseif sign == -1
			append!(TV,[[t[2],t[1],t[3]] for t in triangles] )
		end
	end
	return TV
end
@}
%-------------------------------------------------------------------------------

\paragraph{Map 3-cells to local 2-chain bases}

%-------------------------------------------------------------------------------
@D Map 3-cells to local bases
@{# Map 3-cells to local bases
function map_3cells_to_localbases(V,CV,FV,EV,cscCF,cscFE)
	local3cells = []
	for c=1:length(CV)
		cf = findnz(cscCF[c+1,:])
		tv = LARLIB.triangulate(cf,V,FV,EV,cscFE,cscCF)
		vs = sort(collect(Set(hcat(tv...))))
		vsdict = Dict([(v,k) for (k,v) in enumerate(vs)])
		tvs = [[vsdict[t[1]],vsdict[t[2]],vsdict[t[3]]] for t in tv]
		v = hcat([V[:,w] for w in vs]...)
		cell = [v,tvs]
		append!(local3cells,[cell])
	end
	return local3cells
end
@}
%-------------------------------------------------------------------------------

\paragraph{Visualize solid (explosed) cells}

%-------------------------------------------------------------------------------
@D Visualize solid cells
@{# Visualize solid cells
function viewsolidcells(sx=1.2, sy=1.2, sz=1.2)
	scaling = [sx; sy; sz]
	function viewsolidcells0(V,CV,FV,cscCF,cscFE)
		local3cells = map_3cells_to_localbases(V,CV,FV,EV,cscCF,cscFE)
		hpcs = Any[]
		for local3cell in local3cells
			v,tv = local3cell
			centroid = sum(v,2)/size(v,2)
			scaledcentroid = scaling.*centroid
			translation = scaledcentroid - centroid
			w = v .+ translation
			hpc = p.SOLIDIFY(LARVIEW.lar2hpc(w,tv))
			append!(hpcs, [hpc])
		end
		p.VIEW(p.STRUCT(hpcs))
		return viewsolidcells0
	end
end
@}
%-------------------------------------------------------------------------------



@O lib/jl/LARLIB.jl
@{module LARLIB

	@< LAR imports @>
	@< LAR types @>
	@< Computation of sparse characteristic matrices $M_p$ @>
	@< Computation of (signed) sparse boundary $C_1 \to C_0$ @>
	@< Computation of (unsigned) sparse boundary $C_2 \to C_1$ @>
	@< Computation of (signed) sparse boundary $C_2 \to C_1$ @>
	@< Chain 3-complex construction @>
	@< Collect LAR models in a single LAR model @>
	@< Triangulation of a single facet @>
	@< Triangulation of the 2-skeleton @>
	@< Map 3-cells to local bases @>
	@< Visualize solid cells @>

	include("./utilities.jl")
	include("./minimal_cycles.jl")
	include("./dimension_travel.jl")
	include("./planar_arrangement.jl")
	include("./spatial_arrangement.jl")
	include("./largrid.jl")
	
end
@}

%%%%%%%%%%%%%%%
\section{Standard types}

We define at the top of our module the standard types
that will be used throughout LAR. As already explained
in the introduction [ref. \ref{sec:LAR}], LAR needs
only one bi-dimensional array to store geometry and 
one or more sparse matrices for topology.
Julia has already implemented CSC sparse matrices in
its standard library so we are going to use them.

@D LAR types
@{const Verts = Array{Float64, 2}
const Cells = SparseMatrixCSC{Int8, Int}
const Cell = SparseVector{Int8, Int}
const LarCells = Array{Array{Int, 1}, 1}
@}

We used the general name \texttt{Cells}, but
we are going to use this type also for boundaries.

\subsection{Floating point error}
\label{sec:floating-point_error}

We stored geometry using 64-bit IEEE floats.
As it is known, floating point arithmetic is not
precise and introduces numerical errors.
Usually this is not an issue\footnote{The \textit{machine epsilon},
which is the upper bound on the relative error in floating-point 
arithmetic, for double precision IEEE floating-point numbers is 
$2^53 \approx 1.11 \times 10^{-16}$.}, but when precision is
a goal, floating point error must be handled very carefully.
During the development we encountered several numerical problems
and we tried various approaches (like normalizing the geometry
inside the $[0, 1]$ interval for each dimension in order to maximize
the significand of the floating-point numbers) but most of them turned 
out to be unstable. So we choose the less orthodox path we could
possibly take: we set a fixed error and we performed every floating point
comparison using this error. Examples of this ``tweak'' are to be found in
\ref{sec:intersect_edges}, \ref{sec:face_int}, \ref{sec:3d_minimal_cycles} and 
\ref{sec:vertex_equality}.

%%%%%%%%%%%%%%%%%
\section{Notes on variables names}

Here a list of some often used variable names.

\begin{description}[align=right,labelwidth=2em]
    \item [\texttt{V}:]
        Bi-dimensional array (\texttt{Verts}) that keeps the geometry of a complex.
        Its dimensions are $n \times d$, where $n$ is the number of vertices and $d$ is the dimension
        of the euclidean space in which the complex is embedded.
    \item [\texttt{EV}:]
        1-boundary. It is a $m \times n$ sparse matrix (\texttt{Cells}) 
        where $m$ is the number of edges and $n$ is the number of vertices. The possible values
        are $0, 1$ and $-1$.
    \item [\texttt{FE}:]
        2-boundary. Same as \texttt{EV}, but faces on the rows and edges on the columns.
    \item [\texttt{CF}:]
        3-boundary. Same as \texttt{EV}, but 3-cells on the rows and faces on the columns.
\end{description}

%%%%%%%%%%%%%%%
\section{Tests and examples}

There are several unit tests throughout the implementation. They
are inside the \texttt{test} directory and can be run at once
by executing \texttt{test/runtests.jl}

@O test/jl/runtests.jl
@{using LARLIB

include("./planar_arrangement.jl")
include("./dimension_travel.jl")
include("./largrid.jl")
include("./utilities.jl")
@}

%--------------------------------------------------------------------------------
\subsection{Unit tests on LARLIB.jl}
%--------------------------------------------------------------------------------

%-------------------------------------------------------------------------------
@O test/jl/larlib.jl
@{
@@testset "Boundary operators Tests" begin
   @@testset "characteristicMatrix" begin
      V,cells = LARLIB.larCuboids([2,1,1],true) 
      VV,EV,FV,CV = cells
      @@test typeof(V)==Array{Float64,2}
      @@test typeof(cells)==Array{Array{Array{Int64,1},1},1}
      @@testset "$rel" for rel in cells
         @@test typeof(rel)==Array{Array{Int64,1},1}
         @@test typeof(LARLIB.characteristicMatrix(rel))==SparseMatrixCSC{Int8,Int64}
      end
   end

   @@testset "boundary1 Tests" begin
      V,cells = LARLIB.larCuboids([2,1,1],true) 
      VV,EV,FV,CV = cells
      @@testset "$rel" for rel in cells
         @@test typeof(rel)==Array{Array{Int64,1},1}
         @@test typeof(LARLIB.boundary1(rel))==SparseMatrixCSC{Int8,Int64}
         @@test size(LARLIB.boundary1(EV))==(size(V,2),length(EV)) 
      end
   end

   @@testset "uboundary2 Tests" begin
      V,cells = LARLIB.larCuboids([2,1,1],true) 
      VV,EV,FV,CV = cells
      @@test typeof(LARLIB.uboundary2(FV,EV))==SparseMatrixCSC{Int8,Int64}
      @@test size(LARLIB.uboundary2(FV,EV))==(length(FV),length(EV)) 
   end

   @@testset "boundary2 Tests" begin
      V,cells = LARLIB.larCuboids([2,1,1],true) 
      VV,EV,FV,CV = cells
      @@test typeof(LARLIB.boundary2(FV,EV))==SparseMatrixCSC{Int8,Int64}
      @@test size(LARLIB.boundary2(FV,EV))==(length(FV),length(EV)) 
      @@test sum(findnz(LARLIB.boundary2(FV,EV))[3])==0
   end

   @@testset "chaincomplex Tests" begin
      V,cells = LARLIB.larCuboids([2,1,1],true) 
      VV,EV,FV,CV = cells
      bases,coboundaries = LARLIB.chaincomplex(V,FV,EV)[2:3]
      @@test typeof(bases)==Tuple{Array{Array{Int64,1},1},Array{Array{Int64,1},1},
         Array{Array{Int64,1},1}}
      @@test typeof(coboundaries)==Tuple{SparseMatrixCSC{Int8,Int64},
         SparseMatrixCSC{Int8,Int64},SparseMatrixCSC{Int8,Int64}}
      @@testset "$basis" for basis in bases
         @@test typeof(basis)==Array{Array{Int64,1},1}
      end
      @@testset "$coboundary" for coboundary in coboundaries
         @@test typeof(coboundary)==SparseMatrixCSC{Int8,Int64}
      end
   end
   
   @@testset "collection2model Tests" begin
      @@test typeof(collection)==Array{Array{Array,1},1}
   end

   @@testset "facetriangulation Tests" begin
      V,(VV,EV,FV,CV) = LARLIB.larCuboids([2,2,1],true)
      W,FW,EW = copy(V),copy(FV),copy(EV)
      collection = Array{Array,1}[]
      for k=1:2
    	 W,FW,EW = copy(W)+.5,copy(FV),copy(EV)
    	 append!(collection, [[W,FV,EV]])
      end
      V,FV,EV = LARLIB.collection2model(collection)
      V,bases,coboundaries = LARLIB.chaincomplex(V,FV,EV)
      EV,FV,CV = bases
      cscEV,cscFE,cscCF = coboundaries
      TV = triangulate((1:length(FV),ones(length(FV))),V,FV,cscFE,cscCF)
      @@test typeof(TV)==Array{Array{Int64,1},1}
      @@test typeof((V,FV))==Tuple{Array{Float64,2},Array{Array{Int64,1},1}}
      @@test typeof((1:length(FV),ones(length(FV))))==Tuple{UnitRange{Int64},Array{Float64,1}}
      @@testset "$triangle" for triangle in TV
         @@test length(triangle)==3
      end
   end
end
@}
%-------------------------------------------------------------------------------




Also general examples of some main functionalities are provided.
They can be found into \texttt{examples/general\_examples.jl}

@O examples/jl/general_examples.jl
@{using LARLIB

@< planar\_arrangement general examples @>
@< spatial\_arrangement general examples @>
@}